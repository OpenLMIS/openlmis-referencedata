import org.apache.tools.ant.filters.ReplaceTokens

def ramlToCopy = copySpec {
    from('src/main/resources') {
        include 'api-definition.yaml'
        into 'static/referencedata/docs'
        rename { 'api-definition.raml' }
        filter(ReplaceTokens, tokens: [
                baseUrl: "http://localhost",
                version: project.version
        ])
        filter { line ->
            line.replaceAll("\"schemas(.*).json\"", "\"#/schemas\$1\"")
        }
    }

    from('src/main/resources') {
        include 'api-definition.yaml'
        rename { 'api-definition-raml.yaml' }
        filter(ReplaceTokens, tokens: [
                baseUrl: "http://localhost",
                version: project.version
        ])
    }

    from('src/main/resources/schemas') {
        include '*.json'
        into 'schemas'
    }

    // those files are needed by Api Console
    from('src/main/resources/schemas') {
        include '*.json'
        into 'static/referencedata/docs/schemas'
    }

    from('src/main/resources/schemas/fhir') {
        include '*.json'
        into 'static/referencedata/docs/schemas/fhir'
    }

    // those files are needed by IT
    from('src/main/resources/schemas/fhir') {
        include '*.json'
        into 'schemas/fhir'
        rename '([A-Z][a-z]+).schema.json', '$1'
    }
}

tasks.register('copyRamlToBuild', Copy) {
    group = 'documentation'
    with ramlToCopy
    into 'build/resources/main'
}

tasks.register('ramlToHtml', Exec) {
    group = 'documentation'
    description = 'Convert RAML to HTML document'

    dependsOn 'npmInstall', 'copyRamlToBuild'

    commandLine 'npm', 'run', 'runApiHtmlConverter'
}

tasks.register('copyRamlHtmlToBuild', Copy) {
    group = 'documentation'
    dependsOn 'ramlToHtml'
    from 'build/resources/main'
    include 'api-definition.html'
    into 'build/resources/main'
}
